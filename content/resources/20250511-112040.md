- [<>](https://joinemm.dev/blog/yubikey-nixos-guide#top)
- [Use cases](https://joinemm.dev/blog/yubikey-nixos-guide#use-cases)
- [Pam module](https://joinemm.dev/blog/yubikey-nixos-guide#pam-module)
- [Hardening](https://joinemm.dev/blog/yubikey-nixos-guide#hardening)
- [Sharing](https://joinemm.dev/blog/yubikey-nixos-guide#sharing)
- [Enable for sudo and login](https://joinemm.dev/blog/yubikey-nixos-guide#enable-for-sudo-and-login)
- [GPG keypair](https://joinemm.dev/blog/yubikey-nixos-guide#gpg-keypair)
- [Git commit signing](https://joinemm.dev/blog/yubikey-nixos-guide#git-commit-signing)
- [Decrypting secrets](https://joinemm.dev/blog/yubikey-nixos-guide#decrypting-secrets)
- [Sources](https://joinemm.dev/blog/yubikey-nixos-guide#sources)

# Getting the Most Out of Your Yubikey on NixOS

July 1, 2024

1671 Views

•

- [#nix](https://joinemm.dev/blog?tag=nix)
- [#security](https://joinemm.dev/blog?tag=security)

![yubikey.jpg](https://joinemm.dev/_next/image?url=%2Fimg%2Fblog%2Fyubikey.jpg&w=1920&q=75)

I recently bought two [Yubikey 5](https://www.yubico.com/us/product/yubikey-5-series/yubikey-5-nfc/) devices. One will be on my keychain, and the other will be a backup at home.

Firefox works with the Yubikeys automatically, so I can easily add them to my online accounts. Besides using them for two-factor authentication (2FA) in my browser, I wanted to use them at the operating system level.

For more information about what Yubikeys can do, check out this Reddit post: ["What the heck is a Yubikey and why did I buy one?": A user guide](https://www.reddit.com/r/Yubikey/comments/n8wr55/what_the_heck_is_a_Yubikey_and_why_did_i_buy_one/)

## [Use cases](https://joinemm.dev/blog/yubikey-nixos-guide#use-cases)

I found many ways to use Yubikeys. The ones I have working are marked. I don't necessarily want or support the ones I haven't done. This post will be updated as I find more uses. But I might forget, so check my NixOS config [here](https://github.com/joinemm/nix-infra/blob/master/modules/yubikey.nix) for the latest.

- 2fa for websites (works automatically) ✅
- `pam` auth (sudo/login) ✅
- GPG keys ✅
- Git commit signing ✅
- Encrypting/Decrypting sops secrets ✅
- SSH keys ❓
- Yubikey-based full disk encryption ( [guide](https://nixos.wiki/wiki/Yubikey_based_Full_Disk_Encryption_(FDE)_on_NixOS)) ❓

## [Pam module](https://joinemm.dev/blog/yubikey-nixos-guide#pam-module)

There are two PAM modules for Yubikeys: `yubico-pam` and `pam_u2f`. The second one, `pam_u2f`, is newer and recommended. I tried `yubico-pam` but couldn't get it to work.

> The `pam_u2f` module uses the U2F (universal second factor) protocol. Yubico, Google, and NXP created it, and the FIDO Alliance now manages it as an open standard. Most Yubikeys support U2F, making it the best way to use them for login.

I used the `pam_u2f` module, which lets the Yubikey replace your password for sudo/login. The configuration is declarative, and I can enroll my Yubikeys, including the backup, on another device without having all the keys with me. How?

1.  Plug in your Yubikey.
2.  Create authorization mappings for your keys. This file is like `ssh/known_hosts` and isn't a secret.

```
nix-shell -p pam_u2f
pamu2fcfg -n -o pam://yubi
```

3.  The command will seem to freeze. Touch your Yubikey to continue. A public key for your Yubikey will be printed in the terminal. This key changes every time you run the command. Save it for later.
4.  Repeat steps 1-3 for your other Yubikeys.

Enable `u2f` in your NixOS configuration:

```
security.pam.u2f = {
  enable = true;
  settings = {
    interactive = true;
    cue = true;
  };
};
```

- `interactive`: asks you to `Insert your U2F device, then press ENTER.`
- `cue`: prints `Please touch the device.` when you need to take action.

### [Hardening](https://joinemm.dev/blog/yubikey-nixos-guide#hardening)

The `pam_u2f` guides tell you to save the key mappings in `~/.config/u2f_keys`. I suggest you don't, as it's a security risk. I'm surprised I haven't seen anyone else mention this:

If someone has physical access to your machine (which is already bad), they could plug in their Yubikey, run the commands, and add their keys to your mapping file. This would give them sudo privileges _without your password_. That's terrible!

How to fix this? Don't store the key mappings in a user-writable location. A better place is `/etc/u2f-mapping`. My configuration creates a file in the read-only `/nix/store`.

### [Sharing](https://joinemm.dev/blog/yubikey-nixos-guide#sharing)

If you run `pamu2fcfg` with the default settings, the origin will be `pam://$HOSTNAME`. This means the keys only work on that machine (or others with the same hostname). That's why I chose a more general origin: `pam://yubi`. You can use anything you want.

Now that the keys can be used anywhere, they can be put into the NixOS configuration:

```
security.pam.u2f.settings = {
  origin = "pam://yubi";
  authfile = pkgs.writeText "u2f-mappings" (lib.concatStrings [\
    username\
    ":<KeyHandle1>,<UserKey1>,<CoseType1>,<Options1>"\
    ":<KeyHandle2>,<UserKey2>,<CoseType2>,<Options2>"\
  ]);
};
```

I'm using the `lib.concatStrings` function to put the keys on different lines and add my username (from elsewhere) at the beginning. This just makes it easier to read and isn't required.

### [Enable for sudo and login](https://joinemm.dev/blog/yubikey-nixos-guide#enable-for-sudo-and-login)

The only thing left is to enable the U2F module for the services you want. I've enabled it for sudo and login:

```
security.pam.services = {
  login.u2fAuth = true;
  sudo.u2fAuth = true;
};
```

## [GPG keypair](https://joinemm.dev/blog/yubikey-nixos-guide#gpg-keypair)

Add the following to your NixOS system configuration:

```
services = {
  pcscd.enable = true;
  udev.packages = [ pkgs.yubikey-personalization ];
};
```

And add this to your `home-manager` configuration. Most of this isn't required, but it's good for security.

`disable-ccid` is important to prevent conflicts between the pcscd daemon and gpg-agent.

```
programs.gpg = {
  enable = true;

  # https://support.yubico.com/hc/en-us/articles/4819584884124-Resolving-GPG-s-CCID-conflicts
  scdaemonSettings = {
    disable-ccid = true;
  };

  # https://github.com/drduh/config/blob/master/gpg.conf
  settings = {
    personal-cipher-preferences = "AES256 AES192 AES";
    personal-digest-preferences = "SHA512 SHA384 SHA256";
    personal-compress-preferences = "ZLIB BZIP2 ZIP Uncompressed";
    default-preference-list = "SHA512 SHA384 SHA256 AES256 AES192 AES ZLIB BZIP2 ZIP Uncompressed";
    cert-digest-algo = "SHA512";
    s2k-digest-algo = "SHA512";
    s2k-cipher-algo = "AES256";
    charset = "utf-8";
    fixed-list-mode = true;
    no-comments = true;
    no-emit-version = true;
    keyid-format = "0xlong";
    list-options = "show-uid-validity";
    verify-options = "show-uid-validity";
    with-fingerprint = true;
    require-cross-certification = true;
    no-symkey-cache = true;
    use-agent = true;
    throw-keyids = true;
  };
};

services.gpg-agent = {
  enable = true;

  # https://github.com/drduh/config/blob/master/gpg-agent.conf
  defaultCacheTtl = 60;
  maxCacheTtl = 120;
  pinentryPackage = pkgs.pinentry-curses;
  extraConfig = ''
    ttyname $GPG_TTY
  '';
};
```

Follow this [guide](https://github.com/drduh/YubiKey-Guide) to create a GPG master key with three subkeys: signing, encryption, and authentication. After creating the keys, back them up on a USB stick. Once you have backups, move the keys to the Yubikey. The guide explains this in detail.

You can create a shell with all the packages you need to follow the guide (if you've already deployed the configuration above):

```
nix-shell -p yubikey-manager cryptsetup
```

If you added the public key URL to the Yubikey, you can fetch the public key on any computer you plug the key into:

```
gpg --edit-card
gpg/card> fetch
gpg/card> quit

gpg --edit-key $KEYID
gpg> trust
Your decision? 5
Do you really want to set this key to ultimate trust? (y/N) y
gpg> quit
```

Now the Yubikey can be used as your GPG keypair!

## [Git commit signing](https://joinemm.dev/blog/yubikey-nixos-guide#git-commit-signing)

We can set up Git to use our new GPG key to sign commits. Using home-manager (this can also be done in the regular Git config file):

```
programs.git = {
  userEmail = "same as your key identity"
  signing.key = "$KEYID";
  extraConfig.commit.gpgsign = true;
};
```

Now, when I `git commit`, the GPG key is taken from my Yubikey, and the commit is signed. If my Yubikey isn't plugged in, the commit will fail.

## [Decrypting secrets](https://joinemm.dev/blog/yubikey-nixos-guide#decrypting-secrets)

The Yubikey can be used to decrypt secrets using the GPG key. I use `sops-nix` to manage my NixOS host secrets, and `sops` accepts GPG keys instead of age keys. In `.sops.yaml`:

```
keys:
  - &user 87ECDD306614E5105299F0D4090EB48A4669AA54
  - &host age123456x

creation_rules:
  - path_regex: hosts/host/secrets.yaml$
    key_groups:
      - pgp:
        - *user
        age:
        - *host
```

Here, I have two keys defined. One is for my user, which is my GPG key fingerprint (found with `gpg --list-secret-keys`), and the age key is from the SSH key of a remote host.

I've set both keys as possible decryption keys for `hosts/host/secrets.yaml` in their groups (`pgp` and `age`).

Now, when I open `secrets.yaml`, my GPG key is read from the Yubikey, and the file is decrypted. If my Yubikey isn't plugged in, I can't decrypt the file.

## [Sources](https://joinemm.dev/blog/yubikey-nixos-guide#sources)

[https://nixos.wiki/wiki/Yubikey](https://nixos.wiki/wiki/Yubikey)

[https://developers.yubico.com/pam-u2f](https://developers.yubico.com/pam-u2f)

[https://github.com/drduh/YubiKey-Guide](https://github.com/drduh/YubiKey-Guide)
